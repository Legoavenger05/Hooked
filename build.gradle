buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        jcenter()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_gradle_version"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'kotlin'

version = mod_version
group = 'dev.thecodewarrior.hooked'
archivesBaseName = 'hooked'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-java-parameters",
                "-Xjvm-default=all"
        ]
    }
}

//Print out JVM information so that we know what version is running. Extreamly useful for people to know when helping you.
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: mc_mappings_channel, version: mc_mappings_version

    runs {
        client {
            workingDirectory project.file('run')
            singleInstance true

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                hooked {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            singleInstance true

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                hooked {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            jvmArg('-XstartOnFirstThread')
            singleInstance true

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'hooked', '--all',
                    '--existing', '"' + file('src/main/resources/') + '"',
                    '--output', '"' + file('src/generated/resources/') + '"'

            mods {
                hooked {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main {
        ext.refMap = "dev.thecodewarrior.hooked-refmap.json"
        resources.srcDir(file('src/generated/resources'))
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven { url = 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://minecraft.curseforge.com/api/maven/' }
    maven { url = "https://maven.theillusivec4.top/" }
    maven {
        name = 'kotlinforforge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"

    [
            "foundation",
            "etcetera",
            "glitter"
    ].each {
        compile("com.teamwizardry.librarianlib:librarianlib-$it:$librarianlib_version")
    }
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    annotationProcessor "org.spongepowered:mixin:$mixin_version:processor"
    testAnnotationProcessor "org.spongepowered:mixin:$mixin_version:processor"
    implementation "thedarkcolour:kotlinforforge:$kotlinforforge_version"
}

jar {
    manifest {
        attributes([
            "Implementation-Title": "Hooked",
            "Implementation-Version": "${mod_version}",
            "Implementation-Vendor" :"thecodewarrior",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConnector": "dev.thecodewarrior.hooked.MixinConnector"
        ])
    }
}
